#!/usr/bin/perl

use strict;
use warnings;

use XML::Feed;
use Net::Songkick;
use DateTime;

my $user = shift || 'davorg';
my $feed = XML::Feed->new('Atom');
$feed->title("Upcoming gigs for $user");
$feed->link("http://songkick.com/users/$user");
$feed->modified(DateTime->now);

my $sk = Net::Songkick->new({
  api_key => $ENV{SONGKICK_API_KEY}
});

my $evnts = $sk->get_upcoming_events({
  user => $user,
});

foreach (@$evnts) {
  my $e = XML::Feed::Entry->new('Atom');
  $e->title($_->performances->[0]->artist->displayName);
  $e->link($_->uri);
  $e->content($_->performances->[0]->artist->displayName . ' at ' .
                  $_->venue->displayName);
  $e->issued($_->start);
  $e->modified($_->start);
  $feed->add_entry($e);
}

print $feed->as_xml;
